plugins {
  id "java-library"
  id "maven-publish"
}

group = "cloud.cosmin.lib"

repositories {
  mavenCentral()
}

dependencies {
  implementation "org.apache.commons:commons-lang3:3.1"
  testCompile "org.testng:testng:6.1.1"
}

test {
  // enable TestNG support (default is JUnit)
  useTestNG()

  afterSuite { desc, result ->
    if (!desc.parent) {
      println ":${project.name} -- Executed ${result.testCount} tests: ${result.successfulTestCount} succeeded, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped"
    }
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    urlDetector(MavenPublication) {
      from components.java

      artifact sourcesJar
      artifact javadocJar
    }
  }

  repositories {
    maven {
      name = "onawh.im"
      url = "file://${buildDir}/repo"
    }
  }
}